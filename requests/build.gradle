apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

bintray {
    user = ''
    key = ''
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'requests'
        userOrg = 'basers'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/codebasepk/requestsj.git'
        version {
            name = '0.6'
            released = new Date()
        }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dependencies {
        api 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    }
}

task sourcesJar(type: Jar) {
//    classifier = 'sources' deprecated
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompileProvider.get();
//        variant.javaCompile.classpath.files *obsolete*
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc' deprecated
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

project.archivesBaseName = 'requests'
group = 'pk.codebase.requests'
version = '0.6'
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId 'pk.codebase.requests'
                artifactId 'requests'
                name 'requests'
                description = 'Requests for Android'
                url 'https://github.com/codebasepk/requestsj'

                licenses {
                    license {
                        name 'The GNU General Public License, Version 3.0'
                        url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id 'codebasepk'
                        name 'CodeBasePK'
                        email 'codebasepk@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/codebasepk/requestsj.git'
                    developerConnection 'https://github.com/codebasepk/requestsj'
                    url 'https://github.com/codebasepk/requestsj'
                }
            }
        }
    }
}
